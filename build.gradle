import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java'
    id 'distribution'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.7.6'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
}

ext {
    kafkaVersion = '2.4.1'
    solaceJavaAPIVersion = '10.6.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

testSets {
    integrationTest
}

dependencies {
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.8.0'
    integrationTestImplementation 'org.mockito:mockito-junit-jupiter:3.12.4'
    integrationTestImplementation 'org.testcontainers:testcontainers:1.16.0'
    integrationTestImplementation 'org.testcontainers:junit-jupiter:1.16.0'
    integrationTestImplementation 'com.solace.test.integration:pubsubplus-junit-jupiter:0.4.0'
    integrationTestImplementation 'org.slf4j:slf4j-api:1.7.32'
    integrationTestImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    integrationTestImplementation 'org.apache.commons:commons-configuration2:2.6'
    integrationTestImplementation 'commons-beanutils:commons-beanutils:1.9.4'
    integrationTestImplementation 'com.google.code.gson:gson:2.3.1'
    integrationTestImplementation 'commons-io:commons-io:2.4'
    integrationTestImplementation 'com.squareup.okhttp3:okhttp:4.9.1'
    integrationTestImplementation 'org.apache.kafka:kafka-clients:$kafkaVersion'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.0'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    compile "org.apache.kafka:connect-api:$kafkaVersion"
    compile "com.solacesystems:sol-jcsmp:$solaceJavaAPIVersion"
}

spotbugs {
    effort 'max'
    reportLevel 'high' // Decrease to medium once medium errors are fixed
}

task('jacocoFullReport', type: JacocoReport) {
    description 'Generates code coverage report for all tests.'
    executionData tasks.withType(Test)
    sourceSets sourceSets.main
    reports {
        xml.required = true
    }
}

task('prepDistForIntegrationTesting') {
    dependsOn assembleDist
    doLast {
        copy {
            from zipTree(file('build/distributions').listFiles().findAll {it.name.endsWith('.zip')}[0])
            into (file('src/integrationTest/resources'))
        }
        copy {
            from zipTree(file('build/distributions').listFiles().findAll {it.name.endsWith('.zip')}[0])
            into (file('build/resources/integrationTest'))
        }
    }
}

project.integrationTest {
    useJUnitPlatform()
    outputs.upToDateWhen { false }
    dependsOn prepDistForIntegrationTesting
    afterSuite { desc, result ->
      if (!desc.parent)
          println("${result.resultType} " +
              "(${result.testCount} tests, " +
              "${result.successfulTestCount} successes, " +
              "${result.failedTestCount} failures, " +
              "${result.skippedTestCount} skipped)")
      }
}

project.test {
    useJUnitPlatform()
}

tasks.withType(SpotBugsTask) {
    reports {
        sarif {
            enabled = true
        }
    }
}

distributions {
    main {
        contents {
            from('etc/solace_source.properties') { into 'etc' }
            from('etc/solace_source_properties.json') { into 'etc' }
            from('doc/distribution-readme.md') { into 'doc' }
            from('LICENSE') { into 'doc' }
            from('THIRD-PARTY-LICENSES') { into 'doc' }
            into('lib') {
                from jar
                from(project.configurations.runtime)
            }
            // from jar
        }
    }
}
